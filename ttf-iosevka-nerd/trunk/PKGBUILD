# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Tommaso Sardelli <lacapannadelloziotom@gmail.com>
# Contributor: Sri Harsha <toletysriharsha9@gmail.com>

pkgname=ttf-iosevka-nerd
pkgver=2.3.1
pkgrel=1
pkgdesc='Typeface family designed for coding, terminal use and technical documents (Nerd Fonts)'
arch=(any)
url=https://github.com/ryanoasis/nerd-fonts
license=(MIT)
provides=(ttf-font-nerd)
conflicts=(nerd-fonts-iosevka)
replaces=(nerd-fonts-iosevka)
source=("iosevka-${pkgver}.zip::${url}/releases/download/v${pkgver}/Iosevka.zip")
sha256sums=('68e4575f0831b9c32f8add5e3842a4ee4c1d4f980362ced83fbac8149ef4083a')

prepare() {
  find "${srcdir}" -name '*Windows*' -delete
}

package() {
  install -Dm644 *.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
