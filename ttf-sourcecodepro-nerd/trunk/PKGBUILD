# Maintainer: T.J. Townsend <blakkheim@archlinux.org
# Contributor: P Sherbet <psherbet at psherbet dot com>

pkgname=ttf-sourcecodepro-nerd
pkgver=2.3.0
pkgrel=1
pkgdesc="Patched font Source Code Pro from nerd-fonts library"
arch=("any")
url="https://github.com/ryanoasis/nerd-fonts"
license=("MIT")
provides=(ttf-font-nerd)
conflicts=(nerd-fonts-source-code-pro)
replaces=(nerd-fonts-source-code-pro)
source=("sourcecodepro-${pkgver}.zip::${url}/releases/download/v${pkgver}/SourceCodePro.zip")
sha256sums=('9f0d196e9edec1b9fa6f11edc67b4600ab3e3ebd008d641a3135a9297247be7d')

prepare() {
  find "${srcdir}" -name '*Windows*' -delete
}

package() {
  install -Dm644 *.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
